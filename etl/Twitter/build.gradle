/**
 * Builds Twitter Spark job.
 * 
 * SPARK_LIB must be defined on the command line 
 * gradle build -PSPARK_LIB=/path/to/assembly.jar
 */
buildscript {
	repositories {
		mavenCentral()
	}
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'eclipse'

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.postgresql:postgresql:9.4-1201-jdbc41'
	compile 'org.twitter4j:twitter4j-core:4.0.3'
	compile ('org.elasticsearch:elasticsearch-hadoop:2.1.0.rc1') {
		exclude group: 'org.apache.pig', module: 'pig'
		exclude group: 'cascading', module: 'cascading-local'
		exclude group: 'org.scala-lang', module: 'scala-library'
		exclude group: 'org.apache.hadoop', module: 'hadoop-streaming'
		exclude group: 'org.apache.storm', module: 'storm-core'
		exclude group: 'org.codehaus.jackson', module: 'jackson-mapper-asl'
		exclude group: 'org.apache.spark', module: 'spark-core_2.10'
		exclude group: 'org.apache.hadoop', module: 'hadoop-tools'
		exclude group: 'org.apache.spark', module: 'spark-sql_2.10'
		exclude group: 'org.apache.hive', module: 'hive-service'
		exclude group: 'cascading', module: 'cascading-hadoop'
	}
	compile files("$SPARK_LIB")
}

/* 
 * Use Spark built-in scala library contained within the assembly.
 */
compileScala {
	scalaClasspath = files("$SPARK_LIB")
}

eclipse {
	pathVariables 'GRADLE_USER_HOME': gradle.gradleUserHomeDir
	classpath {
		defaultOutputDir = new File('build/classes')
		file {
			// Remove SCALA_CONTAINER. Use Spark built-in scala library instead.
			whenMerged { classpath ->
				classpath.entries.removeAll { entry ->
					entry.path == "org.scala-ide.sdt.launching.SCALA_CONTAINER"
				}
			}
		}
	}
}

sourceSets {
	main {
		java {
			srcDir 'src/java'
		}
		scala {
			srcDir 'src/scala'
		}
		resources {
			srcDir 'src/resources'
		}
	
		output.classesDir = 'build/classes'
	}
}

jar {
	baseName = 'datawalks-twitter'
	version = '1.0'
}
